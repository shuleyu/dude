#!/bin/csh
# EJG/PL March 2010
# 
#===========================================================
#                  c09.profile_zoom_distinc_sum 
#===========================================================
# this script is the same as c08, except in addition to
# making record sections that are aligned on 
# some seismic phase of interest, the records are summed 
# (stacked) over some distance increment (user defined), then
# the stack traces are plotted.
# a fortran 90 code converts SAC fmt files to ascii, 
# after some quick processing in SAC, then plot the 
# result with GMT.  
#
# the f90 code was written by Pei-ying (Patty) Lin at
# ASU. any errors are undoubtedly mine, as I modified things.

# INPUT:  (1) station/event location info and distances from
# -----       master info file
#         (2) plot box bounds: Dist min/max, time min/max
#         (3) some directory location information
#         (4) reference phase of interest, and component
#             of motion for plotting
#
# known bugs: if all the sac header variables are not
# defined as the f90 code things they should be, you'll
# get a lot of crap spewing to the screen.  oh well.  it
# still works.  ;)


# OUTPUT:  (1) postscript plot for phase_comp
# -----    (2) postscript plot for phase_comp, low passed
#
# KNOWN BUG
# the fortran code that grabs information from
# the SAC files complains about some of the SAC
# header fields having undefined values.  But
# this is some kind of a protocol bug and does 
# not affect any output.  The error scrolls to
# standard out as, for example:
# "ERROR 1336: Undefined header field value. KT6"

echo " "
echo "-------------------------------------------------------------"
echo "                c09.profile_zoom_distinc_sum: STARTING"
echo "-------------------------------------------------------------"

set EVENT     = $1
set DIRSRC    = $2
set DIRDATA   = $3
set DIRPLOT   = $4
set DISTMIN   = $5
set DISTMAX   = $6
set TIMEMIN   = $7
set TIMEMAX   = $8
set PHASE     = $9 
set COMP      = $10
set DISTWIN   = $11
set DISTINC   = $12

# go to the data directory
set DIRPWD = ` pwd `
cd $DIRDATA/$EVENT
source $DIRSRC/c0.ENV_setting

# compute the travel time curves for this series of plots
echo "$DIRSRC/c98.TraveltimeCurve_zoom $EVENT $DIRSRC $DIRDATA $DIRPLOT $DISTMIN $DISTMAX $TIMEMIN $TIMEMAX $PHASE $COMP c09" >! run_TC.sh
csh  run_TC.sh < /dev/null  &

# define the master file location / name
set MASTER = $DIRDATA/$EVENT/eventStation.$EVENT

# loose the header line, and distance out of our range:
awk ' NR>1 && $3>='$DISTMIN' && $3<='$DISTMAX' {print $0}' $MASTER >! INFIL

# define event coords and mag
set COORDS = ` awk ' NR==1 {print $11,$12,$13,$16}' INFIL `
set NSTA   = ` wc -l INFIL | awk '{print $1}' `
set ELAT   = $COORDS[1] 
set ELON   = $COORDS[2] 
set EZ     = $COORDS[3] 
set EMAG   = $COORDS[4] 

# plot Z, and R, and T as separate plots.  We will also
# do a band pass filter (in SAC). We will make two
# versions: raw and low pass filtered.
# thus we are making 6 plots: Z, R, T raw, and Z,
# R, T low passed.

# define the corners for the "raw" data in the band
# pass filter.  this is here for those that wish
# to get rid of the long period noise in the traces
# do it with a band pass, thus, 2 corners needed. 
set B1 = 0.01
set B2 = 1.0

# define the low pass corner:
set F = 0.05

# make the awkfile that will be run by SAC. it is
# basically how we will precondition our records before
# our fortran90 code that converts to ascii:
echo "c09.profile_zoom_distinc_sum: making the SAC macro"
cat << EOF >! awkfile
{
print "r *." \$1 ".?H${COMP}.sac"  
print "rmean" 
print "rtr" 
print "taper" 
print "bp co $B1 $B2 n 2 p 2" 
print "interp d 0.05"
print "w " \$1".$COMP " 
print "lp co $F n 2 p 2" 
print "w " \$1".${COMP}lp" 
} 
EOF

# make the sac macro now, using our awkfile
awk -f awkfile INFIL >! $EVENT.m.process
# run the sac macro
echo "c09.profile_zoom_distinc_sum: running the SAC macro"
sac << END >! out.sac
m $EVENT.m.process
quit
END

# make list of seismograms, for our f90 code
ls *.${COMP} >! ${COMP}.LIST
ls *.${COMP}lp >! ${COMP}lp.LIST

# time to convert files to ASCII, using an f90 code
# written by Pei-ying (Patty) Lin, PhD student in the
# geophysics/seismology group at ASU. code sac2xyzoom.f90
# makes output called "xy.seismograms".
# amplitude scaling is pre-determined, but can be
# changed with AMPSCALE:
set AMPSCALE = 1.0

# compile code:
#$FCOMP -m32 -I$DIRSRC $DIRSRC/sac2xyzoomstack.f90 $SACLIBs -o SAC2XYZOOMSTACK
$FCOMP  -I$DIRSRC $DIRSRC/sac2xyzoomstack.f90 $SACLIBs -o SAC2XYZOOMSTACK

# run the code
foreach TYPE ( $COMP ${COMP}lp )
echo "c09.profile_zoom_distinc_sum: converting $TYPE SAC files to XY"
SAC2XYZOOMSTACK << EOF >! out.sac2xyzoomstack
$PHASE
$TIMEMIN $TIMEMAX
$DISTMIN $DISTMAX
$AMPSCALE
$TYPE.LIST
$DISTWIN $DISTINC
EOF
mv xy.seismograms xy.$TYPE.seismograms

# grab info for the histogram plot to right of profile
awk ' $1==">" { print $3,$2}' xy.$TYPE.seismograms >! $TYPE.stackcount
end
# note: the output is a big file


#===========================================================
#             PLOTTING TIME!!!
#===========================================================

echo "c09.profile_zoom_distinc_sum: MAKING THE PLOT FILES"

# plot the text of the title,
# also, plot a big title at top of page:
# first, break apart event name for Big Title:
set YYYY = ` echo $EVENT | cut -c1-4 `
set MM   = ` echo $EVENT | cut -c5-6 `
set DD   = ` echo $EVENT | cut -c7-8 `
set HH   = ` echo $EVENT | cut -c9-10 `
set MIN  = ` echo $EVENT | cut -c11-12 `

# plot size (the f90 sac2xy code assumes these are the dimensions
# we are working with)
set XSIZE = 6
set YSIZE = 8.5 

# crunch some axis attributes for plot:
# well put dist on horiz axis, and time on vertical
set BX    = `echo $TIMEMIN $TIMEMAX | awk '{print $2-$1}' | awk '{ if ( $1/100 >= 20 ) {print 500,100} else if ( $1/100 < 10 ) {print 100, 10} else {print 200, 20} }'`
set BY    = `echo $DISTMIN $DISTMAX | awk '{print (int(int(($2-$1)/10)/5)+1)*5 }' |  awk '{print $1, $1/5}'`
set SCALE = X"$XSIZE"i/-"$YSIZE"i
set RANGE = $TIMEMIN/$TIMEMAX/$DISTMIN/$DISTMAX/
set BAXIS = a"$BX[1]"f"$BX[2]"/a"$BY[1]"f"$BY[2]"
set Y0 = -Y1.5i

# now build the plots, one type at a time
foreach TYPE ( $COMP ${COMP}lp )

# define the output file
set OUTFILE = $DIRPLOT/$EVENT.c09.profile_zoomstack.$PHASE.$TYPE.ps

# plot some text
pstext -Jx1i -R0/6/0/9 -K -N -P $Y0 << END >! $OUTFILE
3.0 9.3 20  0 0 CB Event: ${MM}/${DD}/$YYYY ${HH}:${MIN} STACK Phase=$PHASE Comp=$TYPE
3.0 9.0 15  0 0 CB $EVENT  LAT=$ELAT LON=$ELON Z=$EZ Mb=$EMAG NSTA=$NSTA
-0.5 4.5 15 90 0 CB Distance (deg)
3.0 -0.7 15  0 0 CB Time after PREM ${PHASE}-wave time (sec)
END

# more text: script name and time stamp
echo "3.0 8.7 10 0 0 CB SCRIPT: c09.profile_zoom_distinc_sum" >! datetag1
date "+CREATION DATE: %m/%d/%y  %H:%M:%S" >! datetag2
paste datetag1 datetag2 >! datetag3
pstext datetag3 -Jx -R -N -Wored -G0 -O -K >> $OUTFILE

# initiate coords for records
psbasemap -J$SCALE -R$RANGE -B"$BAXIS"WSne -K -O -P >> $OUTFILE

# plot the records
psxy xy.$TYPE.seismograms -J$SCALE -R$RANGE -W0.005i/0 $MGMT -O -K >> $OUTFILE

# plot little histogram to right of profile:
# (after Patty's script!)
set HISTSCALE = X1i/-"$YSIZE"i
set iMAX  = ` awk '{print $1}' $TYPE.stackcount | minmax -C | awk '{print $2}'`
@ Ba_hist = $iMAX / 2
@ Bf_hist = $Ba_hist / 5
set R_hist = 0/$iMAX/$DISTMIN/$DISTMAX
psxy $TYPE.stackcount -J$HISTSCALE -R$R_hist -Ba${Ba_hist}f${Bf_hist}S \
    -SB${DISTINC}u -W1/0 -G200 -X"$XSIZE".1i -N -K -O >>  $OUTFILE

# close up plot
pstext -JX -R -O  << END >> $OUTFILE
END

end

#some clean up, lists, tmp records, etc.
\rm *.? *.?lp .gmt* datetag?
\rm *.LIST xy*seismograms *stackcount
\rm  SAC2XYZOOMSTACK out.sac out.sac2xyzoomstack
\rm INFIL awkfile $EVENT.m.process
\rm run_TC.sh 

# return to our pwd
cd $DIRPWD

echo "-------------------------------------------------------------"
echo "                c09.profile_zoom_distinc_sum:     DONE "
echo "-------------------------------------------------------------"
echo " "

#EOF

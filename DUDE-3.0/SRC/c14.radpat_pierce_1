#!/bin/csh
# EJG/PL March 2010
# 
#===========================================================
#              c14.radpat_pierce_1
#===========================================================
# this script is just like c13.*, except it makes only
# one panel, for one phase of interest.
# and it makes it really big.  here, we do not assume the
# input exists, as it might be for some different phase
# we've not done yet. some wonderful and exotic phase.

# INPUT:  (1) station/event location info and distances from
# -----       master info file
#         (2) strike, dip, rake
#         (3) radiation pattern files made from
#             the script named c12.radiation_pat, named
#             P_radiation, SV_radiation, SH_radiation
#         (4) the phase of interest, and radiation pattern
#              (P, SV, or SH) to plot it on
#         (5) some directory location info

# OUTPUT:  (1) a postscript plot with 1 big beach balls
# -----        & piercing points for the phase of interest

echo " "
echo "-------------------------------------------------------------"
echo "                c14.radpat_pierce_1:     STARTING "
echo "-------------------------------------------------------------"

set EVENT     = $1
set DIRSRC    = $2
set DIRDATA   = $3
set DIRPLOT   = $4
set strike    = $5
set dip       = $6
set rake      = $7
set WAVE      = $8   # phase of interest:
set RADIA     = $9   # radiation pattern to plot it on:

# go to the data directory
set DIRPWD = ` pwd `
cd $DIRDATA/$EVENT

# source our environment variable script
source $DIRSRC/c0.ENV_setting

# define the master file location / name
set MASTER = $DIRDATA/$EVENT/eventStation.$EVENT

# define input file, loosing header line
awk ' NR>1 {print $0}'  $MASTER >! INFIL

# define event coords and mag
set COORDS = ` awk ' NR==1 {print $11,$12,$13,$16}' INFIL `
set NSTA   = ` wc -l INFIL | awk '{print $1}' `
set ELAT   = $COORDS[1] 
set ELON   = $COORDS[2] 
set EZ     = $COORDS[3] 
set EMAG   = $COORDS[4] 

# output plot file:
set OUTFILE = $DIRPLOT/$EVENT.c14.radpat_pierce_$WAVE.ps


set X    = $WAVE
set COMP = $RADIA


# output file exists or not 
if ( ! -e c14_radiation."$X"_"$COMP" ) then
# deg 2 rad
set rad2deg = 57.2957795

# radius of source depth
set ER = ` echo $EZ | awk '{print 6371.0 - $1}' `

# we'll need Vp and Vs at the event depth, in order
# to compute take-off angle from ray parameter.
# we'll use a super decimated PREM model.
set REFMOD = $DIRSRC/PREM_Z,Vp,Vs
set VP = ` awk '$1>='$EZ'-0.05 && $1<='$EZ'+0.05 {print $2}' $REFMOD | awk 'NR==1' `
set VS = ` awk '$1>='$EZ'-0.05 && $1<='$EZ'+0.05 {print $3}' $REFMOD | awk 'NR==1' `

# we'll proceed as follows: grab the station & 
# and dist info from the INFIL, and run taup_time,
# which gives us ray parameter.  for phase
# of interest to be plotted on a rad pat, we will
# build a separate file of the output info
# Once done, we proceed to make the plot
# variables.  just make sure taup understands the names

# initiate temporary phase file:
echo $X >! phase.${X}_$COMP
echo $X $COMP >! c14_radiation."$X"_"$COMP"
# define P or S vel (first letter of phase)
set TYPE = `echo $X | cut -c1`
if($TYPE == P ) set VEL = $VP
if($TYPE == S ) set VEL = $VS

# compfile f90 code to give rad pat amp for
# station theta, az for phase list, to go into
# the rad_info file
$FCOMP $DIRSRC/radiation_th,az.f -o RADTHAZ



echo "c14.radpat_pierce_1: computing ray parameters and take off angles "
# loop: STATIONS
@ i = 1
while($i <= $NSTA)
   # grab info for the station
   set ISTA = ( ` awk ' NR=='$i' {print $3,$5,$9,$10} ' INFIL ` )
   set DIST = $ISTA[1] 
   set AZ   = $ISTA[2] 
   set SLAT = $ISTA[3] 
   set SLON = $ISTA[4] 
   # grab the phase name and ray parameter, put into tmp file:
   taup_time -mod prem -evt $ELAT $ELON -h $EZ -deg $DIST \
       -sta $SLAT $SLON  -ph $X | awk ' NR>5 {print $3,$5}'  >! taup.tmp

   # make/add to phase files
   if(-e phase.tmp) \rm phase.tmp

   # we will keep only the first occurrance (i.e., neglect
   # multiple returns of same phase, like triplications or
   # major arc phases). stick ray param in file
   grep $X taup.tmp | awk 'NR==1 {print $2}' >! phase.tmp

   # make sure something is in the file
   @ CHECK = ` wc -l phase.tmp | awk '{ print $1}' `

   # if we have info, calc the take off angle, and
   # write all the necessary info to the phase file
   if ($CHECK == 1 ) then
        awk '{print $0}' phase.tmp >! RAYPAR
        # take off angle from our beloved snell's law
        gmtmath RAYPAR -C0 $VEL MUL $ER DIV $rad2deg MUL ASIN = ANGLE
        # finally, we arrive at our angle:
        set THETA = ` awk '{print '$rad2deg'*$1}' ANGLE `
        echo $THETA $AZ >>!  phase.${X}_$COMP
     
            # run our RADTHAZ code.  first make input. then run.
            # then def our AMP variable.
            echo $strike $dip $rake >! in.RADTHAZ
            echo $COMP >> in.RADTHAZ
            echo $THETA $AZ >> in.RADTHAZ

            RADTHAZ < in.RADTHAZ >! out.RADTHAZ
            set AMP = ` cat out.RADTHAZ `
            # write same info in an output file for possible later use:
            echo $THETA $AZ $AMP | awk '{ printf "%5.1f %5.1f %7.3f\n", $1,$2,$3}' >>!  c14_radiation."$X"_"$COMP"   
   else if ( $CHECK == 0 ) then
       echo "null" $AZ "null" >>! c14_radiation."$X"_"$COMP"    
endif

   @ i++       # increment to next station
end            # ending the station loop

\rm taup.tmp phase.tmp RAYPAR ANGLE 
\rm RADTHAZ in.RADTHAZ out.RADTHAZ
else
    # -- plot ALL 
    #cat c14_radiation."$X"_"$COMP" | awk '$1 != "null" && $3 != "null" {print $1,$2}' >! phase.${X}_$COMP
    # -- only for TA --
    cat $MASTER | awk '{print $2}' >! network.d
    paste network.d c14_radiation."$X"_"$COMP"  | awk '$2 != "null" && $4 != "null" && $1 == "TA" {print $2,$3}' >! phase.${X}_$COMP 
    \rm network.d
endif #endif c14_radiation."$X"_"$COMP"

# ===========================================
#                   plotting time!
# ===========================================

echo "c14.radpat_pierce_1: MAKING THE PLOT FILE "

# make a color palette table for underlying rad pat
makecpt -Cpolar -T-1/1/0.02 -I -Z >! RAD.cpt
gmtset COLOR_NAN 0/0/0

# set some global parameters
# size of beach balls
set SIZE = 6.0i
# x origin
set X0 = 1.2i
# y origin
set Y0 = 2.5i

# convert the output of radiation.f, i.e., ASCII
# radiation pattern info, to GMT's grd file format, for plotting
xyz2grd $EVENT.${COMP}_radiation -G${COMP}.grd -A -I1/1 -R0/360/0/90

# text labels
# break apart event name for Big Title:
set YYYY = ` echo $EVENT | cut -c1-4 `
set MM   = ` echo $EVENT | cut -c5-6 `
set DD   = ` echo $EVENT | cut -c7-8 `
set HH   = ` echo $EVENT | cut -c9-10 `
set MIN  = ` echo $EVENT | cut -c11-12 `

# define our projection and starting position
psbasemap  -R0/360/0/90 -JPa$SIZE -B0 -X$X0 -Y$Y0 -P -K >! $OUTFILE

# background radiation pattern:
grdimage $COMP.grd -R -JP -CRAD.cpt -P -K -O  >> $OUTFILE
# black contour line at radiation amp = zero
echo "0.0 C" >! CONT
grdcontour $COMP.grd -R -JP -CCONT -W2/150 -P -K -O >> $OUTFILE
# red contour line at radiation amp = -0.1
set DASH = t8_20:0
echo "-0.1 C" >! CONT
grdcontour $COMP.grd -R -JP -CCONT -W0.5/220/0/0$DASH -P -K -O >> $OUTFILE
# blue contour line at radiation amp = +0.1
echo "0.1 C" >! CONT
grdcontour $COMP.grd -R -JP -CCONT -W0.5/0/0/200$DASH -P -K -O >> $OUTFILE
# count the stations for each phase type:
set NP = ` wc phase.${X}_$COMP | awk '{print $1}' `
echo "0 90 18 0 5 CB $X $COMP n=$NP " >! text.tmp
# title text of each beach ball:
pstext text.tmp -R -JP -N -Y0.2i -P -K -O >> $OUTFILE
# tick marks on balls:
psbasemap  -R -JP -Bf15wsne -Y-0.2i -P -K -O  >> $OUTFILE
awk ' NR>1 {print $2,$1}' phase.${X}_$COMP >! theta,az.tmp
# add piercing points:
psxy  theta,az.tmp -R -JP -Sx0.25i -W1/0 -P -K -O >> $OUTFILE

# text at the top of page
pstext  -R0/5/0/5 -JX5i -N -O -P -X2.4i -Y3.0i -K << EOF  >> $OUTFILE
0.5 5    20 0 0 CB ${MM}/${DD}/$YYYY ${HH}:${MIN} $X and $COMP Radiation Pattern
0.5 4.75 16 0 4 CB Lat: $ELAT Lon: $ELON Dep:$EZ km Mag: $EMAG 
0.5 4.5  16 0 4 CB Focal Mech: Strike=$strike Dip=$dip Rake=$rake
0.5 4.25 16 0 6 CB (dashed lines: +/- 10% of maximum)
EOF

# more text: script name and time stamp
echo "0.5 4.00 16 0 6 CB SCRIPT: c14.radpat_pierce_1" >! datetag1
date "+CREATION DATE: %m/%d/%y  %H:%M:%S" >! datetag2
paste datetag1 datetag2 >! datetag3
pstext datetag3 -JX -R -N -G0 -O >> $OUTFILE



# clean up
\rm .gmt* text.tmp theta,az.tmp INFIL datetag?
\rm phase.* CONT $COMP.grd RAD.cpt
# return to our pwd
cd $DIRPWD

echo "---------------------------------------------------------------------------------"
echo "                c14.radpat_pierce_1:   just finished making PLOT FILE : $OUTFILE "
echo "---------------------------------------------------------------------------------"
echo " "

#EOF
